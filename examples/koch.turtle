#
# Tweeked Koch Flake
# Made of 6 (Tweeked) Koch Curves in hexagonal pattern.
#
# Koch Curve
#
# L-System:
#  start: F
#  rules:
#     F -> F+F-F-F+F
#  semantics:
#     F means draw forward
#     + and - mean turn left/right by 90 degrees
#
#

int F(int depth, float dist) {
  if (depth <= 0)
    move(dist)
  else let (d = depth-1, u = 1.3*dist) {
    F(d, u);
    rotate(-90);
    F(d, u);
    rotate(+90);
    F(d, u);
    rotate(+91);  # tweek
    F(d, u);
    rotate(-90);
    F(d, u)
  }    
}

int flake(int count, int depth, float dist) {
  if (count > 0) {
    F(depth,dist);
    rotate(60.0);
    flake(count-1, depth, dist)
  } else 0
}

int tmain() {
  pendown(true);
  flake(6,4,1.0)
}